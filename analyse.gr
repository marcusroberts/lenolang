module Analyze

from "./parser.gr" include Parser
from "list" include List
from "map" include Map

provide exception TypeError(String)

provide enum TType {
  TVoid,
  TInt,
  TString,
  TUnbound,
}

provide let typeAsString = (ttype: TType) => {
  match (ttype) {
    TVoid => "Void",
    TInt => "Int",
    TString => "String",
    TUnbound => "Unbound",
  }
}

provide record TStatement {
  statement: Parser.Statement,
  ttype: TType,
}

provide record TExpression {
  expression: Parser.PExpression,
  ttype: TType,
}

provide record TProgram {
  typed_statements: List<TStatement>,
}

let rec process_expression = (expression: Parser.PExpression, symbols) => {
  let expression_type = match (expression) {
    PNumber{ _ } => TInt,
    PString{ _ } => TString,
    BinaryExpression{ operator, left, right, loc } => {
      let left_expr: TExpression = process_expression(left, symbols)
      let right_expr: TExpression = process_expression(right, symbols)

      if (left_expr.ttype != right_expr.ttype) {
        // if one is unbound, we can set the other side
        if (left_expr.ttype == TUnbound && right_expr.ttype != TUnbound) {
          right_expr.ttype
        } else if (left_expr.ttype != TUnbound && right_expr.ttype == TUnbound) {
          left_expr.ttype
        } else {
          throw TypeError(
            "This expression has type " ++
              typeAsString(right_expr.ttype) ++
              " but an expression was expected of type " ++
              typeAsString(left_expr.ttype),
          )
        }
      } else {

        // return left or right, they are the same

        left_expr.ttype
      }
    },
    PVariable{ name, loc } => {
      match (Map.get(name, symbols)) {
        None => throw TypeError("Accessing an undeclared variable"),
        Some(v) => v,
      }
    },
    PFunction{ arg, body, loc } => {
      // This gets interesting
      // we should pass the arg in as unbound into the symbols
      // (unless it's been bound to a type in future with a : type assignment)
      // but locally, not update the global symbols
      // when we see it's unbound on the other side of a binary expression,
      // if we have the type of the other side, we should set it to that

      // so should take a copy of symbols here and inject the a into it.
      // suddenly we have scopes!

      // for now we keep functions pure, nothing declared outside can go in

      let arg_symbols = Map.make()

      Map.set(arg, TUnbound, arg_symbols)

      let bodyType = process_expression(body, arg_symbols)
      bodyType.ttype
    },
    _ => TVoid,
  }

  let typed: TExpression = { expression, ttype: expression_type }
  return typed
}

let process_statement = (statement: Parser.PStatement, symbols) => {
  let typed: TStatement = match (statement.statement) {
    NullStatement => { statement: statement.statement, ttype: TVoid },
    PrintStatement(_) => { statement: statement.statement, ttype: TVoid },
    PAssign(name, expression) => {
      Map.set(name, process_expression(expression, symbols).ttype, symbols)
      { statement: statement.statement, ttype: TVoid }
    },
    ExpressionStatement(expression) => {
      let typed_expression = process_expression(expression, symbols)
      let typed_statement: TStatement = {
        statement: statement.statement,
        ttype: typed_expression.ttype,
      }
      typed_statement
    },
  }

  return typed
}

let process_statements = (statements: List<Parser.PStatement>, symbols) => {
  let types_statements = List.map(
    stmt => process_statement(stmt, symbols),
    statements
  )
  return types_statements
}

let assign_types = (ast: Parser.Program, symbols) => {
  process_statements(ast.statements, symbols)
}

provide let debug_statement = (stmt: TStatement) => {
  print("Typed statement: " ++ typeAsString(stmt.ttype))
}

provide let analyse = (ast: Parser.Program) => {
  print("Analyzing program")
  let symbols = Map.make()
  let types_tree = assign_types(ast, symbols)

  List.forEach(stmt => debug_statement(stmt), types_tree)

  print("Symbols")

  List.forEach(key => {
    match (Map.get(key, symbols)) {
      None => void,
      Some(ttype) => print(key ++ " " ++ typeAsString(ttype)),
    }
  }, Map.keys(symbols))
}
